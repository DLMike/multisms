
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Типы") Тогда
		Для Каждого Спр Из Метаданные.Справочники Цикл
			Если Параметры.Типы.СодержитТип(Тип("СправочникСсылка." + Спр.Имя)) Тогда
				
				Попытка
					СК = ПолучитьСхемуКомпоновки(Спр.Имя);
					ЕстьСхемаКомпонновки = Истина;
				Исключение
					ЕстьСхемаКомпонновки = Ложь;
				КонецПопытки;
				
				Если ЕстьСхемаКомпонновки = Истина Тогда
					Элементы.СправочникОтбора.СписокВыбора.Добавить(Спр.Имя, Спр.Синоним);
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Попытка
		Выполнить("Элементы.ДекорацияНадпись.АвтоМаксимальнаяШирина = Ложь;");
	Исключение
	КонецПопытки;
		
	Если Элементы.СправочникОтбора.СписокВыбора.Количество() > 0 Тогда
		СправочникОтбора = Элементы.СправочникОтбора.СписокВыбора[0].Значение;
		ИзменитьОтбор();
	КонецЕсли;
	ОбновитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьОтбор()
	
	Если ЗначениеЗаполнено(СправочникОтбора) Тогда
	
		// Получим схему из обработки
		СхемаКомпоновки 	= ПолучитьСхемуКомпоновки(СправочникОтбора);

		// Берем настройки по умолчанию из схемы
		НастройкиКомпоновки = СхемаКомпоновки.НастройкиПоУмолчанию;

		// Инициализируем наш отбор
		Адрес 				= Новый УникальныйИдентификатор();
		URLСхемы 			= ПоместитьВоВременноеХранилище(СхемаКомпоновки, Адрес);
		ИсточникНастроек 	= Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы);

		Отбор.Инициализировать(ИсточникНастроек);
		Отбор.ЗагрузитьНастройки(НастройкиКомпоновки);
		
	Иначе
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоОтбору(Команда)
	
	Оповестить("ОтборСМС", Новый Структура("Таблица, ТипЗначения", ПолучитьРезультат(), СправочникОтбора), ЭтаФорма);
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСхемуКомпоновки(ИмяСправочника)
	
	// Получим схему из обработки
	ОбъектТМП 			= РеквизитФормыВЗначение("Объект");
	Возврат ОбъектТМП.ПолучитьМакет("Отбор" + ИмяСправочника + "УФ");
	
КонецФункции

&НаСервере
Функция ПолучитьРезультат()
	
	СхемаКомпоновки = ПолучитьСхемуКомпоновки(СправочникОтбора);
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;

	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновки, Отбор.Настройки, , ,
		Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ТаблицаЗначений = Новый ТаблицаЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаЗначений);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ТаблицаЗначений.Колонки[0].Имя = "Ссылка";
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаЗначений);
	
КонецФункции

&НаКлиенте
Процедура СправочникОтбораПриИзменении(Элемент)
	
	ИзменитьОтбор();
	ОбновитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимость()
	
	Элементы.ОтборНастройкиОтбор.Видимость = ЗначениеЗаполнено(СправочникОтбора);
	
КонецПроцедуры
